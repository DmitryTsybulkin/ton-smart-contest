{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}

() recv_internal() {
}

(int) tlen (tuple t) asm "TLEN";

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {
  int len_rows_a = matrixA.tlen();
  int len_cols_a = matrixA.at(0).tlen();
  int len_cols_b = matrixB.at(0).tlen();

  tuple result = empty_tuple();

  int i = 0;
  while (i < len_rows_a) {
    int j = 0;
    tuple sub_matrix = empty_tuple();
    while (j < len_cols_b) {
      int sum = 0;
      int k = 0;
      while (k < len_cols_a) {
        sum += matrixA.at(i).at(k) * matrixB.at(k).at(j);
        k += 1;
      }
      sub_matrix~tpush(sum);
      j += 1;
    }
    result~tpush(sub_matrix);
    i += 1;
  }

  result~touch();
  return result;
}

